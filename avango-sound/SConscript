# -*- Mode:Python -*-

##########################################################################
#                                                                        #
# This file is part of AVANGO.                                           #
#                                                                        #
# Copyright 1997 - 2008 Fraunhofer-Gesellschaft zur Foerderung der       #
# angewandten Forschung (FhG), Munich, Germany.                          #
#                                                                        #
# AVANGO is free software: you can redistribute it and/or modify         #
# it under the terms of the GNU Lesser General Public License as         #
# published by the Free Software Foundation, version 3.                  #
#                                                                        #
# AVANGO is distributed in the hope that it will be useful,              #
# but WITHOUT ANY WARRANTY; without even the implied warranty of         #
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the           #
# GNU General Public License for more details.                           #
#                                                                        #
# You should have received a copy of the GNU Lesser General Public       #
# License along with AVANGO. If not, see <http://www.gnu.org/licenses/>. #
#                                                                        #
##########################################################################

import avango.build
from avango.build.oshelper import *

avango.build.Environment.prepend_include_path(Dir('include').abspath)
sound_env = avango.build.Environment()

avango.build.add_library(sound_env, 'avango-osg')
avango.build.add_library(sound_env, 'avango-core')
avango.build.add_library(sound_env, 'avango-python')
    
# Substitutions
sound_env['SUBST'] = {
    'PREFIX': avango.build.get_prefix().abspath,
    'AVANGO_SOUND_DEBUG': int(sound_env['DEBUG']),
#    'AVANGO_SOUND_OSG_SUPPORT': int(sound_env['SOUND_SUPPORT']),
    'PKG_CONFIG_REQUIRES': 'avango-core, avango-osg',
    'AVANGO_SOUND_VERSION': '1.90.0',
    'AVANGO_SOUND_VERSION_MAJOR': '1',
    'AVANGO_SOUND_VERSION_MINOR': '90',
    'AVANGO_SOUND_VERSION_MAINT': '0',
    }

avango_sound_pc = sound_env.Subst('dist/pkg-config/avango-sound.pc.in')
Alias('sound', avango_sound_pc)
Alias('install-sound', sound_env.Install(avango.build.get_pkgconfig_path(), avango_sound_pc))

config_h = sound_env.Subst('include/avango/sound/Config.h.in')
Alias('sound', config_h)
Alias('install-sound', sound_env.Install(avango.build.get_include_path('avango/sound'), config_h))

Alias('all', Alias('sound'))
Alias('install', Alias('install-sound'))

# Subdirs
Export('sound_env')

SConscript ([
    'include/avango/sound/SConscript',
    'src/avango/sound/SConscript',
    'src/tests/SConscript',
    'python/SConscript'
])
