# -*- Mode:Python -*-

##########################################################################
#                                                                        #
# This file is part of AVANGO.                                           #
#                                                                        #
# Copyright 1997 - 2009 Fraunhofer-Gesellschaft zur Foerderung der       #
# angewandten Forschung (FhG), Munich, Germany.                          #
#                                                                        #
# AVANGO is free software: you can redistribute it and/or modify         #
# it under the terms of the GNU Lesser General Public License as         #
# published by the Free Software Foundation, version 3.                  #
#                                                                        #
# AVANGO is distributed in the hope that it will be useful,              #
# but WITHOUT ANY WARRANTY; without even the implied warranty of         #
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the           #
# GNU General Public License for more details.                           #
#                                                                        #
# You should have received a copy of the GNU Lesser General Public       #
# License along with AVANGO. If not, see <http://www.gnu.org/licenses/>. #
#                                                                        #
##########################################################################

import avango.build
import avango.build.oshelper

avango.build.Environment.prepend_include_path(Dir('include').abspath)
avango.build.Environment.prepend_python_path(Dir('src/test_package').abspath)
avango.build.Environment.prepend_library_path(Dir('.').abspath)
avango.build.Environment.prepend_library_path(Dir('src/avango').abspath)
avango.build.Environment.prepend_library_path(Dir('src/avango/script').abspath)
avango_env = avango.build.PythonEnvironment()
avango_env.Append(LIBS=["avangoScript"])
avango.build.add_library(avango_env, 'boost_python')
avango.build.add_library(avango_env, 'avango-core')

Alias('all', Alias('python'))
Alias('check', Alias('check-python'))
Alias('install', Alias('install-python'))

avango_script_library_files = Split("""
    src/avango/script/Init.cpp
    src/avango/script/Types.cpp
    src/avango/script/Update.cpp
    src/avango/script/Script.cpp
    """)
avango_lib_env = avango.build.Environment()
avango_lib_env.Append(CPPDEFINES='AV_PYTHON_LIBRARY')
avango.build.add_library(avango_lib_env, 'boost_python')
avango.build.add_library(avango_lib_env, 'avango-core')
avango_script_library = avango_lib_env.SharedLibrary("avangoScript", avango_script_library_files)
Alias('install-python', Install(avango.build.get_library_path(), avango_script_library))

avango_files = Split("""
    src/avango/_avango.cpp
    src/avango/FieldContainer.cpp
    src/avango/Field.cpp
    src/avango/exceptions.cpp
    src/avango/InputStream.cpp
    src/avango/OutputStream.cpp
    """)

avango_python_files = Split("""
    src/avango/__init__.py
    src/avango/_fieldcontainer.py
    src/avango/_utility.py
    src/avango/nodefactory.py
    """)

avango_module = avango_env.SharedLibrary("src/avango/_avango", avango_files)
Depends(avango_module, avango_python_files)
install_python_files = Install(avango.build.get_python_path('avango'), avango_python_files)
install_python_module = Install(avango.build.get_python_path('avango'), avango_module)
Alias('python', avango_module)
Alias('install-python', [install_python_files, install_python_module])

av_script_files = Split("""
    src/avango/script/_script.cpp
    """)
av_script_python_files = Split("""
    src/avango/script/__init__.py
    src/avango/script/_meta_script.py
    src/avango/script/_container.py
    """)
avango_script_env = avango_env.Clone()
av_script = avango_script_env.SharedLibrary("src/avango/script/_script", av_script_files)
Depends(av_script, avango_script_library)
Depends(av_script, av_script_python_files)
Install(avango.build.get_python_path('avango/script'), av_script_python_files)
Install(avango.build.get_python_path('avango/script'), av_script)
Alias('install-python', avango.build.get_python_path('avango/script'))
Alias('python', av_script)


# Build tests
local_env = avango.build.Environment()
avango.build.add_library(local_env, 'avango-core')
local_env.Append(CPPDEFINES=['AV_MOCKUP_LIBRARY'])
mockfieldcontainer = local_env.SharedLibrary("src/tests/MockFieldContainer.cpp")

local_mock_env = avango_env.Clone()
local_mock_env.Prepend(LIBPATH=[Dir('src/tests').abspath])
local_mock_env.Append(LIBS=['MockFieldContainer'])
mockfieldcontainer_python = local_mock_env.SharedLibrary("src/tests/_mockfieldcontainer.cpp")
Depends(mockfieldcontainer_python, mockfieldcontainer)

test_package_dir = 'src/test_package/'
Install(test_package_dir+'avango/', avango_module)
Install(test_package_dir+'avango/script', av_script)
Install(test_package_dir+'tests/', mockfieldcontainer)
Install(test_package_dir+'tests/', mockfieldcontainer_python)
avango.build.install_python(test_package_dir+'avango/', avango_python_files)
avango.build.install_python(test_package_dir+'avango/script', av_script_python_files)
avango_python_test_files = Split("""
    src/tests/mock.py
    src/tests/TestFieldContainer.py
    src/tests/TestField.py
    src/tests/TestUpdate.py
    src/tests/TestScript.py
    src/tests/TestNodefactory.py
    src/tests/TestContainer.py
    src/tests/TestUtility.py
    src/tests/runtests.py
    """)
avango_python_test_pkg_files = Split("""
    src/tests/apackage/__init__.py
    src/tests/apackage/empty.py
    """)
avango.build.install_python(test_package_dir+'tests/', avango_python_test_files)
avango.build.install_python(test_package_dir+'tests/apackage', avango_python_test_pkg_files)

local_test_env = avango.build.TestEnvironment()
local_test_env.PrependENVPath(avango.build.get_library_search_path_env(), Dir('src/tests').abspath)
check = local_test_env.Alias('test-python', 'src/test_package/tests/runtests.pyc', 'python $SOURCE')
AlwaysBuild(check)
Alias('check-python', check)
local_test_env.Depends(check, test_package_dir)


SConscript('include/avango/python/SConscript')
SConscript('include/avango/python/script/SConscript')

avango.build.make_vcproject(local_env, 'avango-python', 'python')
