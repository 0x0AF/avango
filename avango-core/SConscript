# -*- Mode:Python -*-

##########################################################################
#                                                                        #
# This file is part of AVANGO.                                           #
#                                                                        #
# Copyright 1997 - 2009 Fraunhofer-Gesellschaft zur Foerderung der       #
# angewandten Forschung (FhG), Munich, Germany.                          #
#                                                                        #
# AVANGO is free software: you can redistribute it and/or modify         #
# it under the terms of the GNU Lesser General Public License as         #
# published by the Free Software Foundation, version 3.                  #
#                                                                        #
# AVANGO is distributed in the hope that it will be useful,              #
# but WITHOUT ANY WARRANTY; without even the implied warranty of         #
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the           #
# GNU General Public License for more details.                           #
#                                                                        #
# You should have received a copy of the GNU Lesser General Public       #
# License along with AVANGO. If not, see <http://www.gnu.org/licenses/>. #
#                                                                        #
##########################################################################

import avango.build

avango.build.Environment.prepend_include_path(Dir('include').abspath)
local_env = avango.build.Environment()

# Substitutions
local_env['SUBST'] = {
    'PREFIX': avango.build.get_prefix().abspath,
    'AVANGO_DEBUG': int(local_env['DEBUG']),
    'AVANGO_LOG_LEVEL': local_env['LOG_LEVEL'],
    'AVANGO_DISTRIBUTION_SUPPORT': int(local_env['DISTRIBUTION_SUPPORT']),
    'PKG_CONFIG_REQUIRES': '',
    'AVANGO_VERSION': '1.90.0',
    'AVANGO_VERSION_MAJOR': '1',
    'AVANGO_VERSION_MINOR': '90',
    'AVANGO_VERSION_MAINT': '0',
    }
if local_env['DISTRIBUTION_SUPPORT']:
    local_env['SUBST']['PKG_CONFIG_REQUIRES'] = 'avango-ensemble'

avango_osg_pc = local_env.Subst('dist/pkg-config/avango-core.pc.in')
Alias('core', avango_osg_pc)
local_env.Alias('install-core', local_env.Install(avango.build.get_pkgconfig_path(), avango_osg_pc))

config_h = local_env.Subst('include/avango/Config.h.in')
Alias('core', config_h)
local_env.Alias('install-core', local_env.Install(avango.build.get_include_path('avango'), config_h))

Alias('all', Alias('core'))
Alias('check', Alias('check-core'))
Alias('install', Alias('install-core'))

# Subdirs

SConscript ([
    'include/avango/SConscript',
    'src/avango/SConscript',
])

avango.build.make_vcproject(local_env, 'avango-core', 'core')
