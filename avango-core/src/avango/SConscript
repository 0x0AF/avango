# -*- Mode:Python -*-

##########################################################################
#                                                                        #
# This file is part of AVANGO.                                           #
#                                                                        #
# Copyright 1997 - 2009 Fraunhofer-Gesellschaft zur Foerderung der       #
# angewandten Forschung (FhG), Munich, Germany.                          #
#                                                                        #
# AVANGO is free software: you can redistribute it and/or modify         #
# it under the terms of the GNU Lesser General Public License as         #
# published by the Free Software Foundation, version 3.                  #
#                                                                        #
# AVANGO is distributed in the hope that it will be useful,              #
# but WITHOUT ANY WARRANTY; without even the implied warranty of         #
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the           #
# GNU General Public License for more details.                           #
#                                                                        #
# You should have received a copy of the GNU Lesser General Public       #
# License along with AVANGO. If not, see <http://www.gnu.org/licenses/>. #
#                                                                        #
##########################################################################

import avango.build

subdirs = Split('''
    actions
    fields
    interface
    logging
    network
    nodes
    types
    streams
    support
    ''')

avango_env = avango.build.Environment()

avango_env.Append(LDFLAGS='-export-dynamic') # FIXME does this work on Windows?
avango_env.Append(CPPDEFINES=['AV_LIBRARY'])
for lib in Split('dl boost_thread boost_signals'):
    avango.build.add_library(avango_env, lib)
if avango.build.oshelper.os_is_mac():
	avango.build.add_library(avango_env, "boost_system")
	
if avango_env['DISTRIBUTION_SUPPORT']:
    avango.build.add_library(avango_env, 'avango-ensemble')
    if avango_env['ZMQ_DISTRIBUTION_SUPPORT']:
        avango_env.Append(LIBS=['zmq'])
Export('avango_env')

avango.build.Environment.prepend_library_path(Dir('.').abspath)
avango_test_env = avango.build.TestEnvironment()
avango.build.add_library(avango_test_env, 'avango-core')
Export('avango_test_env')

objects = []
for dir in subdirs:
    objects += SConscript(dir+'/SConscript')

lib = avango_env.SharedLibrary('avango', objects+['Init.cpp'])
Alias('core', lib)
installed_avango_lib = Install(avango.build.get_library_path(), lib)
Alias('install-core', installed_avango_lib)
