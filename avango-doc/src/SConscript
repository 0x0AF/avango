# -*- Mode:Python -*-

##########################################################################
#                                                                        #
# This file is part of Avango.                                           #
#                                                                        #
# Copyright 1997 - 2008 Fraunhofer-Gesellschaft zur Foerderung der       #
# angewandten Forschung (FhG), Munich, Germany.                          #
#                                                                        #
# Avango is free software: you can redistribute it and/or modify         #
# it under the terms of the GNU Lesser General Public License as         #
# published by the Free Software Foundation, version 3.                  #
#                                                                        #
# Avango is distributed in the hope that it will be useful,              #
# but WITHOUT ANY WARRANTY; without even the implied warranty of         #
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the           #
# GNU General Public License for more details.                           #
#                                                                        #
# You should have received a copy of the GNU Lesser General Public       #
# License along with Avango. If not, see <http://www.gnu.org/licenses/>. #
#                                                                        #
# Avango is a trademark owned by FhG.                                    #
#                                                                        #
##########################################################################

import avango.build

# environment
doc_env = avango.build.Environment()

# doxygen sources
doxygen_sources = Split("""
    index.dox
    styleguide.cpp.expl
    styleguide.dox
    styleguide.h.expl
    """)

# doxygen substitutions
doc_env['SUBST'] = {
    'AVANGO_INCLUDE_DIR': avango.build.get_include_path().abspath,
    }

# doxygen build
doxygen_doxyfile = doc_env.Subst('Doxyfile.in')

doxygen_html_latex = doc_env.Command(
    ['html/index.html', 'latex/Makefile'],
    [doxygen_sources, doxygen_doxyfile],
    "cd .. && doxygen Doxyfile",
    chdir = 1)

doxygen_refman = doc_env.Command(
    'latex/refman.pdf',
    doxygen_html_latex,
    "make refman.pdf",
    chdir = 1)

# doxygen clean additions
doc_env.Clean([doxygen_html_latex, doxygen_refman], ['html', 'latex'])

# doxygen install
doxygen_install_html = doc_env.Install(
    avango.build.get_documentation_path().abspath,
    source = 'html')

doc_env.AddPreAction(doxygen_install_html, 'rm -rf ' + avango.build.get_documentation_path().abspath + '/html')

doxygen_install_pdf = doc_env.InstallAs(
    target = avango.build.get_documentation_path().abspath + '/avango-ng.pdf',
    source = 'latex/refman.pdf')

# epydoc build
epydoc_html = doc_env.Command(
    'html/epydoc/index.html',
    '',
    'epydoc --name "Avango-NG Python Bindings" --docformat="restructuredtext" --html'
    + ' --graph="classtree" --src-code-tab-width="2" --exclude-introspect="daemon" -o . '
    + avango.build.get_python_path().abspath + '/avango',
    chdir = 1)

epydoc_latex = doc_env.Command(
    'epydoc-latex/api.pdf',
    '',
    'epydoc --name "Avango-NG Python Bindings" --docformat="restructuredtext" --pdf'
    + ' --no-private --graph="classtree" --src-code-tab-width="2" --exclude-introspect="daemon" -o . '
    + avango.build.get_python_path().abspath + '/avango',
    chdir = 1)

# epydoc clean additions
doc_env.Clean(epydoc_latex, 'epydoc-latex')

# epydoc install
epydoc_install_pdf = doc_env.InstallAs(
    target = avango.build.get_documentation_path().abspath + '/avango-ng-python.pdf',
    source = 'epydoc-latex/api.pdf')

# aliases
Alias('doxygen', [doxygen_refman, doxygen_html_latex])
Alias('epydoc', [epydoc_html, epydoc_latex])
Alias('doc', ['doxygen', 'epydoc'])
Alias('install-doc', ['doc', doxygen_install_html, doxygen_install_pdf, epydoc_install_pdf]);
