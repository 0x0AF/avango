# -*- Mode:Python -*-

##########################################################################
#                                                                        #
# This file is part of Avango.                                           #
#                                                                        #
# Copyright 1997 - 2009 Fraunhofer-Gesellschaft zur Foerderung der       #
# angewandten Forschung (FhG), Munich, Germany.                          #
#                                                                        #
# Avango is free software: you can redistribute it and/or modify         #
# it under the terms of the GNU Lesser General Public License as         #
# published by the Free Software Foundation, version 3.                  #
#                                                                        #
# Avango is distributed in the hope that it will be useful,              #
# but WITHOUT ANY WARRANTY; without even the implied warranty of         #
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the           #
# GNU General Public License for more details.                           #
#                                                                        #
# You should have received a copy of the GNU Lesser General Public       #
# License along with Avango. If not, see <http://www.gnu.org/licenses/>. #
#                                                                        #
# Avango is a trademark owned by FhG.                                    #
#                                                                        #
##########################################################################

import avango.build

# doxygen environment
doxygen_env = avango.build.Environment()

# doxygen sources
doxygen_sources = Split("""
    index.dox
    styleguide.cpp.expl
    styleguide.h.expl
    """)


# doxygen substitutions
doxygen_env['SUBST'] = {
    'AVANGO_INCLUDE_DIR': avango.build.get_include_path().abspath,
    }


# doxygen build
doxygen_doxyfile = doxygen_env.Subst('Doxyfile.in')

doxygen_html_latex = doxygen_env.Command(
    ['html/index.html', 'latex/Makefile'],
    [doxygen_sources, doxygen_doxyfile],
    "cd .. && doxygen Doxyfile",
    chdir = 1)

doxygen_env.Clean(doxygen_html_latex, ['doxygen.log', 'html', 'latex'])

doxygen_refman = doxygen_env.Command(
    'latex/refman.pdf',
    doxygen_html_latex,
    "make refman.pdf",
    chdir = 1)


# doxygen install
doxygen_install_html = doxygen_env.Install(
    avango.build.get_documentation_path().abspath + '/doxygen',
    source = 'html')

doxygen_env.Clean(doxygen_install_html, avango.build.get_documentation_path().abspath + '/doxygen/html')

doxygen_install_pdf = doxygen_env.InstallAs(
    target = avango.build.get_documentation_path().abspath + '/doxygen/avango-cpp-reference.pdf',
    source = 'latex/refman.pdf')


# aliases
Alias('doxygen', [doxygen_refman, doxygen_html_latex])
Alias('install-doxygen', ['doxygen', doxygen_install_html, doxygen_install_pdf]);
