# -*- Mode:Python -*-

##########################################################################
#                                                                        #
# This file is part of AVANGO.                                           #
#                                                                        #
# Copyright 1997 - 2009 Fraunhofer-Gesellschaft zur Foerderung der       #
# angewandten Forschung (FhG), Munich, Germany.                          #
#                                                                        #
# AVANGO is free software: you can redistribute it and/or modify         #
# it under the terms of the GNU Lesser General Public License as         #
# published by the Free Software Foundation, version 3.                  #
#                                                                        #
# AVANGO is distributed in the hope that it will be useful,              #
# but WITHOUT ANY WARRANTY; without even the implied warranty of         #
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the           #
# GNU General Public License for more details.                           #
#                                                                        #
# You should have received a copy of the GNU Lesser General Public       #
# License along with AVANGO. If not, see <http://www.gnu.org/licenses/>. #
#                                                                        #
##########################################################################

import avango.build
import avango.build.oshelper

daemon_env = avango.build.Environment()
daemon_env.Append(CPPDEFINES='AV_DAEMON_LIBRARY')

# Sources section
lib_sources = Split('''
    Device.cpp
    DeviceActuator.cpp
    DeviceDaemon.cpp
    DeviceSensor.cpp
    DeviceService.cpp
    DTrack.cpp
    Init.cpp
    LinuxEvent.cpp
    Station.cpp
    StationBlock.cpp
    StationSegment.cpp
    ''')

if avango.build.oshelper.os_is_windows():
    lib_sources += Split('''
        EventMapping.win.cpp
        HIDInput.win.cpp
        ''')
else:
    lib_sources += Split('''
        HIDInput.linux.cpp
		SharedMemorySegment.cpp
        WacomTablet.cpp
        Wiimote.cpp
        WiimoteActuator.cpp
        ''')

    
if daemon_env['VRPN_SUPPORT']:
    lib_sources += Split('''
        VRPNClient.cpp
        ''')

# append external sources
lib_sources.append(Split('''
    dtrack/dtrack.cpp
    '''))

#bin_source = ['DeviceDaemon.cpp']

# Rules section
if 'icpc' in daemon_env['CXX']:
    daemon_env.Append(CXXFLAGS='-wd593')
avango.build.add_library(daemon_env, 'avango-core')
avango.build.add_library(daemon_env, 'avango-osg')

if avango.build.oshelper.os_is_windows():
    daemon_env.Prepend(LIBS = ['user32'])

if daemon_env['VRPN_SUPPORT']:
    avango.build.add_library(daemon_env, 'vrpn')

lib = daemon_env.SharedLibrary('avangoDaemon', lib_sources)

# Alias section
Alias('daemon', lib)
Alias('install-daemon', Install(avango.build.get_library_path(), lib))
#Alias('install-daemon', Install(avango.build.get_binary_path(), bin))

avango.build.Environment.prepend_library_path(Dir('.').abspath)
